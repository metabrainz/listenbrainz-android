import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        kotlin_version = '1.9.10'
        navigationVersion = '2.7.2'
        hilt_version = '2.48'
        compose_version = '1.5.0'
        room_version = '2.5.2'
        accompanist_version = '0.32.0'
        work_version = "2.8.1"
        exoplayer_version = '2.19.1'
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }
}

plugins {
    id 'com.google.devtools.ksp' version '1.9.10-1.0.13' apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {url "https://jitpack.io"}
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

subprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("composeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
            if (project.findProperty("composeCompilerMetrics") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
        }
    }
}
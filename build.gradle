import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        kotlin_version = '1.9.22'
        navigationVersion = '2.7.7'
        hilt_version = '2.50'
        compose_version = '1.6.1'
        room_version = '2.6.1'
        accompanist_version = '0.34.0'
        work_version = '2.9.0'
        exoplayer_version = '2.19.1'
        paging_version = "3.2.1"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }
}

plugins {
    id 'com.google.devtools.ksp' version '1.9.22-1.0.17' apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {url "https://jitpack.io"}
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

subprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("composeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
            if (project.findProperty("composeCompilerMetrics") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_compiler"
                ]
            }
        }
    }
}